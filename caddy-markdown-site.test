#! /usr/bin/env tclsh

package require Tcl 8.6-10

package require http
package require tcltest


proc main {} {
    cd [file dirname [info script]]

    set caddy caddy
    if {[info exist ::env(CADDY)]} {
        set caddy $::env(CADDY)
    }
    set port 8080
    set url http://localhost:$port

    set pid [run $caddy run &]
    after 500

    tcltest::test index-1.1 {} -body {
        fetch $url
    } -match glob -result {*<title>Welcome*}

    tcltest::test subdir-1.1 {} -body {
        fetch $url/subdir
    } -match glob -result {*index of a subdirectory*}

    tcltest::test extension-1.1 {} -body {
        set a [compress-whitespace [fetch $url/index]]
        set b [compress-whitespace [fetch $url/index.md]]
        list [expr { $a eq $b }] $a $b
    } -match glob -result {1 *}


    fetch http://localhost:2019/stop -method POST

    # Exit with a nonzero status if there are failed tests.
    set failed [expr {$tcltest::numTests(Failed) > 0}]

    tcltest::cleanupTests
    return $failed
}


proc run args {
    exec >@ stdout 2>@ stderr {*}$args
}


proc fetch {url args} {
    try {
        set token [http::geturl $url {*}$args]
        http::data $token
    } finally {
        catch { http::cleanup $token }
    }
}


proc compress-whitespace text {
    regsub -all {(\s)\s+} $text {\1}
}


exit [main]
